Objective = "Create " Number " districts while " MinMax " the number of " ObjectiveType ".";

MinMax = "minimizing"
       | "maximizing";

ObjectiveType = DistrictObjective
              | PrecinctObjective;

DistrictObjective = "districts that " DistrictCondition;
PrecinctObjective = "precincts that " PrecinctCondition;




DistrictCondition = DistrictComparisonCondition
                  | DistrictAttributeCondition;
DistrictComparisonCondition = DistrictDemographicComparisonCondition
                            | DistrictPrecinctComparisonCondition;

DistrictDemographicComparisonCondition = DemographicComparisonCondition;
DistrictPrecinctComparisonCondition = "have " CompareWord " " DistrictPrecinctAttribute " than/as " DistrictPrecinctAttribute;
DistrictAttributeCondition = "have " DistrictAttribute;

DistrictAttribute = DemographicAttribute
                  | DistrictPrecinctAttribute;

DistrictPrecinctAttribute = "precincts that " PrecinctCondition;



PrecinctCondition = PrecinctComparisonCondition
                | PrecinctAttributeCondition;
PrecinctComparisonCondition = PrecinctDemographicComparisonCondition
                          | PrecinctNeighborComparisonCondition;

PrecinctDemographicComparisonCondition = DemographicComparisonCondition;
PrecinctNeighborComparisonCondition = "have " CompareWord " " PrecinctNeighborAttribute " than/as " PrecinctNeighborAttribute;
PrecinctAttributeCondition = "have " PrecinctAttribute;

PrecinctAttribute = DemographicAttribute
                | PrecinctNeighborAttribute;
PrecinctNeighborAttribute = "neighboring precincts that are " NeighborAttribute;



NeighborAttribute = "in the same district"
                  | "in a different district";



DemographicComparisonCondition = "have " CompareWord " " DemographicAttribute " than/as " DemographicAttribute;
DemographicAttribute = "voters who " Demographic;
Demographic = "are Asian"
            | "are Black"
            | "are Caucasian"
            | "are Hispanic"
            | "vote Red"
            | "vote Blue";

CompareWord = "more"
            | "fewer"
            | "the same number of";

Number = ( digit )+;
